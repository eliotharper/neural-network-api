%%[
var @shortLongCode, @sessionDE, @questionLogDE, @answerLogDE, @apiEndpoint, @sessionToken,
    @logging, @newSession, @sessionQuestion, @sessionAnswer, @sessionEnd, @startKeyword

set @shortLongCode = "61409063302"
set @sessionDE = "GW Active Sessions"
set @questionLogDE = "GW Outbound Messages"
set @answerLogDE = "GW Inbound Messages"
set @apiEndpoint = "https://guesswho-api.d121.com"
set @startKeyword = "PLAY"
set @logging = false

if trim(uppercase([MSG(0)])) == @startKeyword
or trim(uppercase([MSG(0).NOUNS])) == @startKeyword then
   set @newSession = true 
   UpsertData(@sessionDE, 1, "mobile", MOBILE_NUMBER, "token", HTTPGET(CONCAT(@apiEndpoint,"/newsession")), "created", SystemDateToLocalDate(Now()), "sessionEnded", 0)
endif

set @sessionToken = Lookup(@sessionDE, "token", "mobile", MOBILE_NUMBER)

if @newSession == true then
   set @sessionQuestion = HTTPGET(CONCAT(@apiEndpoint,"/game/", @sessionToken))
else
   set @sessionQuestion = HTTPGET(CONCAT(@apiEndpoint,"/game/", @sessionToken, "/response/", [MSG(0).NOUNS]))
endif

if @logging == true then
InsertData(@questionLogDE, "mobile", MOBILE_NUMBER, "token", @sessionToken, "message", @sessionQuestion, "created", SystemDateToLocalDate(Now()))
endif

if @newsession != true and @logging == true then
   InsertData(@answerLogDE, "mobile", MOBILE_NUMBER, "token", @sessionToken, "message", [MSG(0).NOUNS], "created", SystemDateToLocalDate(Now()))
endif

if Substring(@sessionQuestion,1,10) == "I think of" then
   set @sessionEnd = true
   set @sessionAnswer = CONCAT("Your character must be: ",Substring(@sessionQuestion,12,100),".")
   UpdateData(@sessionDE, 1, "mobile", MOBILE_NUMBER, "sessionEnded", 1)
   EndSmsConversation(@shortLongCode,MOBILE_NUMBER)
endif
]%%
%%[if @newSession == true then]%%
Think of a real or fictional character.

Reply to questions: "yes", "no", "don't know", "probably", or "probably not".
%%[endif]%%
%%[if @sessionEnd != true then]%%
%%=v(@sessionQuestion)=%%
%%[else]%%
%%=v(@sessionAnswer)=%%

Thanks for playing! Reply "play" to start a new game.
%%[endif]%%
